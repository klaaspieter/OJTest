#!/usr/bin/env objj

@import <OJUnit/OJTestRunnerText.j>
@import <OJCov/OJCoverageRunnerText.j>

var parser = new (require("args").Parser)();

parser.usage("INPUT_FILE ...");
parser.help("Runs the given suite of tests.");

parser.option("-c", "coverage")
    .def(false)
    .set(true)
    .help("Runs test coverage along with your OJUnit tests.");

parser.option("-t", "threshold")
    .def(0.8)
    .set()
    .help("Set the threshold for test coverage to the specified percentage. Implies -c. Default: 0.8");

parser.option("-l", "level")
    .def(@"warn")
    .choices(["fatal", "error", "warn", "info", "debug", "trace"])
    .set()
    .help("Set the minimum log level.");

parser.helpful();

function main(args) {
    var savedArgs = args.slice(0);

    var options = parser.parse(args);

    if (options.args.length < 1)
    {
        parser.printUsage(options);
        return;
    }

    CPLogRegister(CPLogPrint, options.level);

    if (options.coverage || savedArgs.indexOf("-t") !== -1)
    {
        var threshold = options.threshold > 1 ? options.threshold / 100 : options.threshold;

        var runner = [[OJCoverageRunnerText alloc] initWithThreshold:threshold];
    }
    else
    {
        var runner = [[OJTestRunnerText alloc] init];
    }
    
    [runner startWithArguments:options.args];
}
